! Links definitions
option, warn,info;
system,"ln -fns /afs/cern.ch/eng/lhc/optics/runII/2015 db5";

is_thin=0;
mylhcbeam=4; !1: Beam 1, 2: Beam 2 clockwise, 4: Beam 2 anti-clockwise


! sequence and strength file - 2015
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
Option, -echo,-warn,-info;
if (mylhcbeam<3){ call,file="db5/lhc_as-built.seq"; };
if (mylhcbeam>3){ call,file="db5/lhcb4_as-built.seq"; };
if (is_thin==1){call,file="db5/toolkit/slice.madx";};
Option, -echo,warn,info;

if (is_thin==0){
  call, file="db5/opt_inj.madx";
  call, file="db5/opt_800_10000_800_3000.madx";
} else {
  call, file="db5/opt_inj_thin.madx";
  call, file="db5/opt_800_10000_800_3000_thin.madx";
};


! Beam definition
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
if (mylhcbeam<3){
beam, sequence=lhcb1, bv= 1, energy=NRJ,  particle=proton,
  npart=1.2e11,kbunch=2748, ex=7.29767146889e-09,ey=7.29767146889e-09;
beam, sequence=lhcb2, bv=-1, energy=NRj,  particle=proton,
  npart=1.2e11,kbunch=2748, ex=7.29767146889e-09,ey=7.29767146889e-09;
};

if (mylhcbeam>3){
beam, sequence=lhcb2, bv=1, energy=NRj,  particle=proton,
  npart=1.2e11,kbunch=2748, ex=7.29767146889e-09,ey=7.29767146889e-09;
};



! twiss and SixTrack export
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
select, flag=twiss, clear;
select, flag=twiss, column=name,s,keyword,l,betx,bety,x,px,y,py,mux,muy,dx,dpx,dy,dpy,alfx,alfy;
set,    format="12.6f";
 
if (mylhcbeam<3){
seqedit, sequence=lhcb1;
   flatten;
!   cycle, start=IP3;
 endedit;
 use,    period=lhcb1;
 twiss,  sequence=lhcb1, file=twiss_b1.tfs, save;};

if (mylhcbeam>3){
seqedit, sequence=lhcb2;
   flatten;
!   cycle, start=IP3;
 endedit;
 use,    period=lhcb2;
 twiss,  sequence=lhcb2, file=twiss_b2.tfs, save;};


sixtrack, radius=17E-03;


! twiss to print optics at IPs 
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
set,format={"9.6f","-5s"};
select,flag=twiss,clear;
select,flag=twiss, pattern="IP[1258]$",column=name,s,betx,bety,x,y,px,py;

if (mylhcbeam==1){use, sequence=lhcb1;twiss;write,table=twiss;};
if (mylhcbeam==2){use, sequence=lhcb2;twiss;write,table=twiss;};
if (mylhcbeam==4){use, sequence=lhcb2;twiss;write,table=twiss;};

system,"rm db5";
system, "mv fc.2 fort.2";
